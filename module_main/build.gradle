if (isComponentToModuleMain.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        if (isComponentToModuleMain.toBoolean()) {
            applicationId "com.windfallsheng.componentbasedaction.module_main"
        }
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        // Enabling multidex support.
        multiDexEnabled true

        // 这个配置只能限制xml文件里的资源，对于图片资源还是通过命名区别；
        resourcePrefix "main_"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        debug {
//            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
////            signingConfig signingConfigs.releaseConfig
//            //applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    /**
     * java插件引入了一个概念叫做SourceSets，通过修改SourceSets中的属性，
     * 可以指定哪些源文件（或文件夹下的源文件）要被编译，哪些源文件要被排除。
     */
    sourceSets {
        main {
            if (isComponentToModuleMain.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除 module 文件夹中的所有Java文件
                java {
                    exclude 'module/**'
                }
            }
        }
    }

    if (isComponentToModuleMain.toBoolean()) {
        // 指定维度
        flavorDimensions "version"

        productFlavors {
            // 开发环境
            development {
                applicationIdSuffix ".development"
                manifestPlaceholders = [app_name: "module_home", icon: "@mipmap/ic_launcher"]
                buildConfigField("String", "MODE", "\"development\"")
                dimension "version"
            }
            //生产环境
            product {
                manifestPlaceholders = [app_name: "module_home", icon: "@mipmap/ic_launcher"]
                buildConfigField("String", "MODE", "\"product\"")
                dimension "version"
            }
        }

        // 批量打包
        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def fileName = "${applicationId}_${variant.productFlavors[0].name}_v${variant.versionName}_${buildTime()}.apk"
                println "print apk name=" + fileName
                //修改apk文件名称
                outputFileName = fileName
            }
        }
    }
}

//获取编译时间
static def buildTime() {
    return new Date().format("yyyy年MM月dd日HH时mm分ss秒SSS", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation project(path: ':module_base')
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

    implementation 'me.majiajie:pager-bottom-tab-strip:2.3.0X'

}